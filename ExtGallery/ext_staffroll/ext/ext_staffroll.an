;*********************************************************
;@EXT_DESC	文字レイヤを利用したスタッフロール
;@VER		2015.6.30.000
;*********************************************************
; 以降は基本ノータッチで
; 文字レイヤのレイアウト変更とかならアリです
; 要 AIRNovel 5.36以上

;*********************************************************
; 初期化
[macro name="staffroll_init"]
	&save:an.doRecLog = false
	&an.staffroll.bk_dw = sys:an.tagCh.doWait
	&an.staffroll.bk_dwk = sys:an.tagCh.doWait_Kidoku
	&sys:an.tagCh.doWait = false
	&sys:an.tagCh.doWait_Kidoku = false

	[if exp="mp:width != null"]
		&an.staffroll.bp = 'rl'
		&an.staffroll.w = mp:width
	[elsif exp="mp:height != null"]
		&an.staffroll.bp = 'tb'
		&an.staffroll.h = mp:height
	[else]
		[trace text="[staffroll_init] エラー 属性widthかheightを指定して下さい"]
		[break_macro]
	[endif]
	[let name="fontSize" text=%fontSize|24]
	[let name="fcol" text=%fcol|0xFFFFFF]
	&an.staffroll.lay = save:const.an.mesLayer
	[lay layer=&an.staffroll.lay page=%page|fore b_alpha=0 visible=true max_col=35 bura_col=2 max_row=1000 chk_overrow=true t_width=%width|null t_height=%height|null onsplit="enter" layout=#&'lineHeight="36" justificationRule="space" columnGap="0" paddingLeft="70" paddingTop="30" paddingRight="70" paddingBottom="70" verticalAlign="inherit" blockProgression="$an.staffroll.bp" lineBreak="explicit" fontLookup="embeddedCFF" renderingMode="cff" fontSize="$fontSize" locale="ja" kerning="off" trackingRight="0" color="$fcol" whiteSpaceCollapse="preserve"'#]
[endmacro]

;*********************************************************
; スクロール開始
[macro name="staffroll_start"]
	&sys:an.tagCh.doWait = an.staffroll.bk_dw
	&sys:an.tagCh.doWait_Kidoku = an.staffroll.bk_dwk
	[if exp="an.staffroll.bp=='rl'"]
		[antw from=0 to="&-an.staffroll.w" time=%time update=*up_w finish=*fin]
	[else]
		[antw from=0 to=&an.staffroll.h time=%time update=*up_h finish=*fin]
	[endif]
	&an.staffroll.fn = mp:const.an.me_call_scriptFn
	&an.staffroll.finish = mp:finish
[endmacro]

;*********************************************************
; 後始末
[macro name="staffroll_uninit"]
	[antw_finish]
	&save:an.doRecLog = true
[endmacro]

;*********************************************************
[return]

;*********************************************************
*up_w
	[lay layer=&an.staffroll.lay h_scroll=&an.antw.v][return]

*up_h
	[lay layer=&an.staffroll.lay v_scroll=&an.antw.v][return]

;*********************************************************
*fin
	&save:an.doRecLog = true
	[jump fn=&an.staffroll.fn label=&an.staffroll.finish]
