;*********************************************************
;@EXT_DESC	ボイスをより簡単に扱う
;@VER		2017.7.7.001
;*********************************************************

;*********************************************************
;	定数
; 声優さん音声、名前とファイル名対応（人物が増えたら追加）
;	日本語名が英数字名で統一した方がシンプルだが、
;	Webサーバーに置きたいときなど後々困る気がするので
;	スクリプト上の指定とファイル名表記を切り離しておきます
;&const.db_v_who.あみたろ = 'ami'
;&const.db_v_who.円海アルト = 'mar'
;	ここに書くと本拡張ファイル上書きのたびに手直し必要なので、
;	sub.an 冒頭あたりに追記推奨

; 以降は基本ノータッチで
;*********************************************************
; マクロ 音声を再生
&lastVoice = ''	; 初期値・クリア
[macro name="voice" exist_err=false]
	[let name="lastVoice" text=%fn cond="mp:fn != null"]
	[stopse buf=%buf|音声]
	[se * buf=%buf|音声 fn=%fn]
[endmacro]

;*********************************************************
; マクロ 音声待ち
[macro name="vw" exist_err=false]
	[ws buf=%buf|音声]
[endmacro]

;*********************************************************
; マクロ 効果音
[macro name="se" exist_err=false]
	[stopse * cond="mp:fn == null"]
	[playse * cond="mp:fn != null" join=false]
[endmacro]


;*********************************************************
; マクロ 連番ファイル名の音声再生
[macro name="voice2" exist_err=false]
	; この辺に顔アイコン変更とか名前表示とか追加したり
	&fn = const.db_v_who[mp:text]
	[break_macro cond=fn==null]	; 音声無しは無処理

	[let name=c text='&save:db_v_cnt[mp:text] || 1']
	[let_substr name=cnt text="&'000'+ c" pos=-3 len=all cast=str]
	[voice fn="&fn +'_'+ cnt@str"]
	[let name="&'save:db_v_cnt.'+ mp:text" text=&c+1]
[endmacro]
	; 括弧マクロ（この辺は好みだが、使うと便利）
	[bracket2macro text="〔〕" name=voice2]


;*********************************************************
; マクロ クリック待ち・改行
[macro name="lr" exist_err=false]
	[ws buf=%buf|音声 canskip=true cond="an.auto.enabled"]
	[l][r]
	&lastVoice = ''
[endmacro]

;*********************************************************
; マクロ クリック待ち・改ページ
[macro name="pr" exist_err=false]
	[ws buf=%buf|音声 canskip=true cond="an.auto.enabled && !an.skip.enabled"]
	[p visible=%visible|true]
	[er]
	[record_place layer=&dsp_lays]	; ページ冒頭でセーブポイント
	&lastVoice = ''

	; この辺は好み
	[fadeoutse buf="SE" time=%time|500 cond="mp:no_se_stop === null && !an.skip.enabled"]
	[stopse buf="SE" cond="an.skip.enabled"]
	[stopse buf=%buf|音声 cond="mp:no_voice_stop === null || an.skip.enabled"]
[endmacro]

;*********************************************************
; マクロ あの素晴らしい声をもう一度
[macro name="repeat_voice" exist_err=false]
	[voice fn=&lastVoice cond="lastVoice != ''"]
[endmacro]

;*********************************************************

[return]
