;*********************************************************
;@EXT_DESC	ボイスをより簡単に扱う
;@VER		2017.11.30.005	再生をjoin=trueとし、[se]不使用
;@VER		2017.8.24.004	[voice]のみ cache=false とする
;@VER		2017.7.8.003	[se]を sub.an に移動
;@VER		2017.7.7.002	〔名前〕のみで完結するように。
;				カウンタ初期化追記を促すように
;@VER		2015.6.21.001	新規：〔名前〕と『台詞』形式
;*********************************************************

;*********************************************************
;	定数
; キャラ音声、名前とファイル名対応（人物が増えたら追加）
;	日本語名か英数字名で統一した方がシンプルだが、
;	Webサーバーに置きたいときなど後々困る気がするので
;	スクリプト上の指定とファイル名表記を切り離しておきます
;		&const.db_v_who.梨香 = 'rika'
;		&const.db_v_who.紀子 = 'nori'
;		：
;	ここに書くと本拡張ファイル上書きのたびに手直し必要なので、
;	sub.an 冒頭あたりに追記推奨
; 【重要】7/8 初期化について
;	sub.an 内の [sys_title_start] に、必ず以下のような
;	カウンターリセット処理を追記して下さい。（キャラ人数分）
;		&save:db_v_cnt.梨香 = 1
;		&save:db_v_cnt.紀子 = 1
;		：


; 以降は基本ノータッチで
;*********************************************************
; マクロ 音声を再生
&lastVoice = ''	; 初期値・クリア
[macro name="voice"]
	[let name=lastVoice text=%fn cond="mp:fn != null"]
	[stopse buf=%buf|音声]
	[playse * buf=%buf|音声 fn=%fn cache=false]
[endmacro]

;*********************************************************
; マクロ 音声待ち
[macro name="vw"]
	[ws * buf=%buf|音声]
[endmacro]


;*********************************************************
; マクロ 連番ファイル名の音声再生
[macro name="voice2"]
	; この辺に顔アイコン変更とか名前表示とか追加したり
	&fn = const.db_v_who[mp:text]
	[break_macro cond=fn==null]	; 音声無しは無処理

	&c = save:db_v_cnt[mp:text]
	[trace text='【エラー】拡張機能 ext_voice.an 使用時の初期化がされていません。' cond='c==null']
	[trace text='&"【&save:db_v_cnt."+ mp:text +" = 1】がありません。"' cond='c==null']
	[trace text='【対処法】詳しくは同ファイル冒頭の説明文をご覧下さい。' cond='c==null']
	
	[let_substr name=cnt text="&'000'+ c" pos=-3 len=all cast=str]
	[voice fn="&fn +'_'+ cnt@str"]
	[let name="&'save:db_v_cnt.'+ mp:text" text=&c+1]
[endmacro]
	; 括弧マクロ（この辺は好みだが、使うと便利）
	[bracket2macro text="〔〕" name=voice2]


;*********************************************************
; マクロ クリック待ち・改行
[macro name="lr" exist_err=false]
	[vw * canskip=true cond="an.auto.enabled"]
	[l][r]
	&lastVoice = ''
[endmacro]

;*********************************************************
; マクロ クリック待ち・改ページ
[macro name="pr" exist_err=false]
	[vw * canskip=true cond="an.auto.enabled && !an.skip.enabled"]
	[p visible=%visible|true]
	[er]
	[record_place layer=&dsp_lays]	; ページ冒頭でセーブポイント
	&lastVoice = ''

	; この辺は好み
	[fadeoutse buf="SE" time=%time|500 cond="mp:no_se_stop === null && !an.skip.enabled"]
	[stopse buf="SE" cond="an.skip.enabled"]
	[stopse buf=%buf|音声 cond="mp:no_voice_stop === null || an.skip.enabled"]
[endmacro]

;*********************************************************
; マクロ あの素晴らしい声をもう一度
[macro name="repeat_voice"]
	[voice fn=&lastVoice cond="lastVoice != ''"]
[endmacro]

;*********************************************************

[return]
