;*********************************************************
;@EXT_DESC	ボイスをより簡単に扱う
;@VER		2015.6.21.001
;*********************************************************

;*********************************************************
;	定数
; マクロ 連番音声の人物指定管理（人物が増えたら改造必要）
;	日本語名が英数字名で統一した方がシンプルだが、
;	Webサーバーに置きたいときなど後々困る気がするので
;	スクリプト上の指定とファイル名表記を切り離しておきます
&const.db_v_who.あみたろ = 'ami'	; 変更出来ない方が良いのでconst
&const.db_v_who.円海アルト = 'mar'	; 変更出来ない方が良いのでconst
[macro name="voice_reset" exist_err=false]
	&db_v_cnt.あみたろ = 0
	&db_v_cnt.円海アルト = 0
[endmacro]


; 以降は基本ノータッチで
;*********************************************************
&lastVoice = ''	; 初期値・クリア

;*********************************************************
; マクロ 音声を再生
[macro name="voice" exist_err=false]
	[let name="lastVoice" text=%fn cond="mp:fn != null"]
	[stopse buf=%buf|音声]
	[se * buf=%buf|音声 fn=%fn]
[endmacro]

;*********************************************************
; マクロ 音声待ち
[macro name="vw" exist_err=false]
	[ws buf=%buf|音声]
[endmacro]

;*********************************************************
; マクロ 効果音
[macro name="se" exist_err=false]
	[stopse * cond="mp:fn == null"]
	[playse * cond="mp:fn != null" join=false]
[endmacro]


;*********************************************************
; マクロ 連番音声の人物指定
[macro name="voice_set_chr" exist_err=false]
	&save:v_who = mp:name
	&save:v_who_fn = const.db_v_who[mp:name]
[endmacro]

;*********************************************************
; マクロ 連番ファイル名の音声再生
&save:v_who = ''
&save:v_who_fn = ''
[macro name="voice2" exist_err=false]
	&c = db_v_cnt[save:v_who]
	[let_substr name=cnt text="&'000'+ c" pos=-3 len=all cast=str]
	[voice fn="&mp:const.an.me_call_scriptFn +'_'+ save:v_who_fn +'_'+ cnt@str"]
	[let name="&'db_v_cnt.'+ save:v_who" text=&c+1]
[endmacro]


;*********************************************************
; マクロ クリック待ち・改行
[macro name="lr" exist_err=false]
	[ws buf=%buf|音声 canskip=true cond="an.auto.enabled"]
	[l][r]
	&lastVoice = ''
[endmacro]

;*********************************************************
; マクロ クリック待ち・改ページ
[macro name="pr" exist_err=false]
	[ws buf=%buf|音声 canskip=true cond="an.auto.enabled && !an.skip.enabled"]
	[p visible=%visible|true]
	[er]
	[record_place layer=&dsp_lays]	; ページ冒頭でセーブポイント
	&lastVoice = ''

	; この辺は好み
	[fadeoutse buf="SE" time=%time|500 cond="mp:no_se_stop === null && !an.skip.enabled"]
	[stopse buf="SE" cond="an.skip.enabled"]
	[stopse buf=%buf|音声 cond="mp:no_voice_stop === null || an.skip.enabled"]
[endmacro]

;*********************************************************
; マクロ あの素晴らしい声をもう一度
[macro name="repeat_voice" exist_err=false]
	[voice fn=&lastVoice cond="lastVoice != ''"]
[endmacro]

;*********************************************************

[return]
