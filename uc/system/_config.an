	[return cond="const.plgConfig.inited"]
	&const.plgConfig.inited = true


	[sys_syslay.init]
	[loadplugin fn="plgConfig"]

	[event global=true call=true key=c label=*keycall_config]
	[event global=true call=true key=t label=*text_back_alpha_sub]
	[event global=true call=true key=y label=*text_back_alpha_add]
	[event global=true call=true key=u label=*text_auto_msecwait_sub]
	[event global=true call=true key=i label=*text_auto_msecwait_add]

	[let name="sys:TextLayer.Back.Alpha" text=0.5 cond="sys:TextLayer.Back.Alpha == null"]
	[let name="isUserUpdateCheck" text=false cond="isUserUpdateCheck == null"]
[return]

;*********************************************************
*text_back_alpha_sub
	[return cond="! isGameState"]
	&alpha = sys:TextLayer.Back.Alpha *100 -5 = int
	[let name="alpha" text=0 cond="alpha < 0"]
	&sys:TextLayer.Back.Alpha = alpha /100
[jump label=*text_back_alpha]

*text_back_alpha_add
	[return cond="! isGameState"]
	&alpha = sys:TextLayer.Back.Alpha *100 +5 = int
	[let name="alpha" text=100 cond="alpha > 100"]
	&sys:TextLayer.Back.Alpha = alpha /100

*text_back_alpha
	[lay layer="mes" b_alpha=&sys:TextLayer.Back.Alpha]
	[notice text="&'バック不透明度 ' + alpha + '%'" left="&const.flash.display.Stage.stageWidth¥const.an.retinaRate -10 -300" top=10]
	[return]

;*********************************************************
*text_auto_msecwait_sub
	[return cond="! isGameState"]
	&wait = sys:an.auto.msecLineWait -100 = uint
	[let name="wait" text=0 cond="wait < 0"]
[jump label=*text_auto_msecwait]

*text_auto_msecwait_add
	[return cond="! isGameState"]
	&wait = sys:an.auto.msecLineWait +100 = uint
	[let name="wait" text=3500 cond="wait > 3500"]
*text_auto_msecwait
	&sys:an.auto.msecLineWait = wait
	[notice text="&'自動読み進み速度 ' + wait + ' msec'" left="&const.flash.display.Stage.stageWidth¥const.an.retinaRate -10 -300" top=10]
[return]

;*********************************************************
*title_config
	[enable_event enabled=false]
	&呼び元 = "Title"
[jump label=*main]

;*********************************************************
*keycall_config
	[se fn=&sysse_ok1 buf=SYS]

*rclick_config
	[return cond="! isGameState"]

	&呼び元 = "Text"

;*********************************************************
*main
	[enable_event enabled=false]
	[clear_event]
	[lay layer="_syslay.bg" fn="_config" visible=false float=true]
		[zoom_tsy layer="_syslay.bg" time=400 visible=true]
	[config_refresh]
		[plugin name="plgConfig" visible=false float=true]
		[zoom_tsy plugin="plgConfig" time=400 visible=true]

	[lay layer="_syslay.mes" visible=false b_alpha=0 r_size=6 max_col=20 bura_col=2 max_row=4 layout='lineHeight="18" paddingTop="6" paddingLeft="839" fontSize="12" color="0x000000" fontFamily="IPA明朝cff,_等幅" fontLookup="embeddedCFF"']	; PC版
;--	[lay layer="_syslay.mes" visible=false b_alpha=0 r_size=6 max_col=20 bura_col=2 max_row=4 layout='lineHeight="18" paddingTop="6" paddingLeft="330" fontSize="12" color="0x000000" fontFamily="IPA明朝cff,_等幅" fontLookup="embeddedCFF"']	; Mobile版
		[ch layer="_syslay.mes" wait=0 record=false text="&'ver '+ tmp:const.flash.desktop.NativeApplication.nativeApplication.applicationDescriptor.versionNumber@str" cond="const.flash.system.Capabilities.playerType == 'Desktop'"]
			;#FONT	ver0.123456789
		
		[button layer="_syslay.mes" page=fore pic="Mnu_btnUpdate" left=816 top=16 global=true call=true label=*update_check hint="アプリの更新を確認します" enterse=&sysse_choice cond="const.flash.system.Capabilities.playerType == 'Desktop'"]	; PC版
		[button layer="_syslay.mes" page=fore pic="Mnu_btnReturn" left=916 top=16 global=true label=*exit hint="戻る" enterse=&sysse_choice clickse=&sysse_cancel]
		
		[button layer="_syslay.mes" page=fore pic="Mnu_btnExport" left=25 top=710 global=true call=true label=*export hint="ゲーム情報を書き出す" enterse=&sysse_choice cond="const.flash.system.Capabilities.playerType == 'Desktop'"]	; PC版
		[button layer="_syslay.mes" page=fore pic="Mnu_btnImport" left=125 top=710 global=true call=true label=*import hint="ゲーム情報を読み込む" enterse=&sysse_choice cond="const.flash.system.Capabilities.playerType == 'Desktop'"]	; PC版
		
		[lay layer="_syslay.mes" page=fore visible=true float=true]


	[let name="isGameState" text=false]
*retry
	[event key=rightclick label=*cancel]
	[event key=command label=*cancel]
	[event key=control label=*cancel]
	[event key=end label=*cancel]
	[event key=shift label=*cancel]
	[event key=c label=*cancel]
	[gesture_event swipe=*nop f2tap=*cancel f3tap=*cancel]
	[set_focus to=null]
[s]

*nop
[jump label=*retry]


;*********************************************************
*cancel
	[se fn=&sysse_cancel buf=SYS]
*exit
	[zoom_tsy plugin="plgConfig" visible=false time=400]	; PC版
;--	[plugin name="plgConfig" visible=false]		; Mobile版
	[clear_lay layer="_syslay.mes" page=both]
	[zoom_tsy layer="_syslay.bg" visible=false time=400]	; PC版
;--	[lay layer="_syslay.bg" visible=false]		; Mobile版

	[enable_event enabled=true]
	[set_focus to=null]
	[let name="isGameState" text=true cond="呼び元=='Text'"]
[return]

;*********************************************************
*update_check
	[return cond="isUserUpdateCheck"]
	&isUserUpdateCheck = true
	[se fn=&sysse_ok1 buf=SYS]

	[update_check url="http://famibee.cs.land.to/an/UnderCherry/ver.xml"]
[return]

;*********************************************************
*export
	[se fn=&sysse_ok1 buf=SYS]
	[export path="app-storage:/bookmark/*"]
[return]

;*********************************************************
*import
	[se fn=&sysse_ok1 buf=SYS]
	[import]
	[return cond="! const.an.dlgUserOK"]

	[enable_event enabled=false]
		[clear_text layer="mes_yesno"]
		[ask_ync mes="ゲームを再起動する必要があります。終了しますか？　（起動は手動で）"]
			;#FONT	ゲームを再起動する必要があります。終了しますか？　（起動は手動で）
	[enable_event enabled=true]
	[return cond="_yesno != 'y'"]

	[call label=*exit]
	[wait_tsy plugin="plgConfig"]
[close]
